group = 'de.ars.daojones.gradle.plugins'
version = '2.0.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

sourceCompatibility = 1.7

jar {
	manifest {
		attributes 'Implementation-Title': 'DaoJones Gradle Plugin',
				   'Implementation-Version': version
	}
}

repositories {
	maven {
		url "http://buildart.ars.de:8081/artifactory/ars-int-release-local"
	}
	mavenCentral()
	maven {
		url "http://buildart.ars.de:8081/artifactory/ars-ext-release-local"
	}
	mavenLocal()
}

dependencies {
	compile gradleApi()
	compile group: 'de.ars.daojones', name: 'daojones-runtime', version: version
	testCompile group: 'junit', name: 'junit', version: '4.12'
	testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
	runtime group: 'org.reflections', name: 'reflections', version: '0.9.9-RC1'
	runtime group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.2'
}

configure(install.repositories.mavenInstaller) {
	pom.artifactId = 'daojones-gradle-plugin'
}


task sourceJar(type: Jar) {
	from sourceSets.main.allJava
	classifier "sources"
}

publishing {
	publications {
		jars(MavenPublication) {
			from components.java
			artifact sourceJar
			pom.withXml {
			  asNode().appendNode('description', 'Gradle Plugin for DaoJones projects.')
			}
	   }
	}
	repositories {
		maven {
			url "http://buildart.ars.de:8081/artifactory/ars-int-release-local"
			credentials {
				username = "deployer"
				password = "password"
			}
			name = "ars-int-release-local"
		}
	}
}
